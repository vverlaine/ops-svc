plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'org.springframework.boot' version '3.3.4' apply false
}

subprojects {
    group = 'com.proyecto.ops'
    version = '0.1.0'
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

def serviceProjects = subprojects.findAll { it.name != 'common' && it.name != 'web-portal' }

configure(serviceProjects) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation project(':common')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        runtimeOnly 'org.postgresql:postgresql'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}
